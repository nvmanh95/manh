buildscript {
    ext.springBootVersion = '2.1.3.RELEASE'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
    }
}

allprojects {
    buildDir = file("${rootDir}/build/${name}")

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete file('build')
}

subprojects {

    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext["spock.version"] = "1.1-groovy-2.4"

    jacoco {
        toolVersion = "0.8.2"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.16.16'
        compile 'org.apache.commons:commons-lang3:3.4'
        compile 'org.apache.commons:commons-collections4:4.0'
        compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
        compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.8.0'

        testCompile 'junit:junit:4.12'
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }

    jar {
        destinationDir = file("${rootDir}/build/libs")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR1"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '5.3'
}